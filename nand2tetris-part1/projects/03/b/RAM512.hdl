// This file is part of the materials accompanying the book 
// "The Elements of Computing Systems" by Nisan and Schocken, 
// MIT Press. Book site: www.idc.ac.il/tecs
// File name: projects/03/b/RAM512.hdl

/**
 * Memory of 512 registers, each 16 bit-wide. Out holds the value
 * stored at the memory location specified by address. If load==1, then 
 * the in value is loaded into the memory location specified by address 
 * (the loaded value will be emitted to out from the next time step onward).
 */

CHIP RAM512 {
    IN in[16], load, address[9];
    OUT out[16];

    PARTS:
    	// upper 3 bits of address indicate which RAM64 to use, lower 6 indicate position in that guy

    	DMux8Way(in=load, sel=address[6..8], a=loadInnerRam0, b=loadInnerRam1, c=loadInnerRam2, d=loadInnerRam3, e=loadInnerRam4, f=loadInnerRam5, g=loadInnerRam6, h=loadInnerRam7);

    	RAM64(in=in, load=loadInnerRam0, address=address[0..5], out=outInnerRam0);
    	RAM64(in=in, load=loadInnerRam1, address=address[0..5], out=outInnerRam1);
    	RAM64(in=in, load=loadInnerRam2, address=address[0..5], out=outInnerRam2);
    	RAM64(in=in, load=loadInnerRam3, address=address[0..5], out=outInnerRam3);
    	RAM64(in=in, load=loadInnerRam4, address=address[0..5], out=outInnerRam4);
    	RAM64(in=in, load=loadInnerRam5, address=address[0..5], out=outInnerRam5);
    	RAM64(in=in, load=loadInnerRam6, address=address[0..5], out=outInnerRam6);
    	RAM64(in=in, load=loadInnerRam7, address=address[0..5], out=outInnerRam7);

    	Mux8Way16(a=outInnerRam0, b=outInnerRam1, c=outInnerRam2, d=outInnerRam3, e=outInnerRam4, f=outInnerRam5, g=outInnerRam6, h=outInnerRam7, sel=address[6..8], out=out);
}